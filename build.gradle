buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group = "com.zehir.biomestweaker" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.mod_id

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    
    replaceIn "reference/R.java"
    replace "@VERSION@", "${config.mod_version}"
    if (project.hasProperty("biomestweaker_signature")) {
        replace "@FINGERPRINT@", project.biomestweaker_signature
    }
    
    assetDir = "eclipse/assets"
}

version = "${config.minecraft_version}-${config.mod_version}"

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

processResources {
    // exclude xcf files, as they are for development only
    exclude '**/*.xcf'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'

        // replace version and mcversion
        expand 'mod_version': project.version, 'minecraft_version': project.config.minecraft_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}

artifacts {
    archives devJar
}

import net.minecraftforge.gradle.delayed.*
import net.minecraftforge.gradle.tasks.dev.ChangelogTask

task createChangelog(type: ChangelogTask) {

    if (("${System.getenv().JOB_NAME}" != null) && project.hasProperty("jenkins_server") && project.hasProperty("jenkins_password")) {
        def jobName = "${System.getenv().JOB_NAME}"
        def buildNumber = "${System.getenv().BUILD_NUMBER}"

        setServerRoot(new DelayedString(project, project.jenkins_server))
        setJobName(new DelayedString(project, jobName.toString()))
        setAuthName(new DelayedString(project, project.jenkins_user))
        setAuthPassword(new DelayedString(project, project.jenkins_password))
        setTargetBuild({ buildNumber.toString() });
        setOutput(new DelayedFile(project, 'build/libs/' + project.archivesBaseName + '-' + project.version + '-changelog.txt'));
    }

    onlyIf {
        return (("${System.getenv().JOB_NAME}" != null) && project.hasProperty("jenkins_server") && project.hasProperty("jenkins_password"))
    }
}

tasks.build.dependsOn('createChangelog')
tasks.build.dependsOn('signJar')

if (!project.hasProperty("keystore_location")) // keystore stuff
    ext.keystore_location = "."
if (!project.hasProperty("keystore_password")) // keystore stuff
    ext.keystore_password = ""
if (!project.hasProperty("biomestweaker_keystore_alias")) // keystore stuff
    ext.biomestweaker_keystore_alias = ""

task signJar(dependsOn: ["reobf", "devJar"]) {
    inputs.dir jar.destinationDir
    inputs.file keystore_location
    inputs.property "biomestweaker_keystore_alias", biomestweaker_keystore_alias
    inputs.property "keystore_password", keystore_password
    outputs.dir devJar.destinationDir
    
    // only sign if the keystore exists
    onlyIf {
        return keystore_location != "." && keystore_password != ""
    }
    // the actual action.. sign the jar.
    doLast {
        jar.destinationDir.eachFile { file ->
            if (!file.getPath().endsWith(".jar"))
                return; // skip non-jars

            logger.lifecycle "signing $file"
            ant.signjar(
                destDir: file.getParentFile(), // same place it came from
                jar: file,
                keystore: keystore_location,
                alias: biomestweaker_keystore_alias,
                storepass: keystore_password
            )
       }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("forgemaven_url")) {
                logger.info('Publishing to files server')
                repository(url: project.forgemaven_url) {
                    authentication(userName: project.forgemaven_username, password: project.forgemaven_password)
                }
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }

            pom.project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Biomes Tweaker'
                url 'https://github.com/Zehir/BiomesTweaker/'

                scm {
                    url 'https://github.com/Zehir/BiomesTweaker/'
                    connection 'scm:git:git://github.com/Zehir/BiomesTweaker.git'
                    developerConnection 'scm:git:git@github.com/Zehir/BiomesTweakergit'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/Zehir/BiomesTweaker/issues'
                }

                licenses {
                    license {
                        name 'GNU Lesser General Public License 3.0'
                        url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'Zehir'
                        name 'Zehir'
                        roles {
                            role 'developer'
                        }
                    }
                }
            }
        }
    }
}
